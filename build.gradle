import org.apache.tools.ant.filters.*

plugins {
	id 'java'
	id 'application'
}

sourceCompatibility = 1.8
version = '0.0.0-pre'
mainClassName = 'hr.vsite.hive.HiveCmd'

run {
	classpath += files("${System.env.HIVE_CONF}")
}

startScripts {
	doLast {
		unixScript.text = unixScript.text.replace('CLASSPATH=', 'CLASSPATH=$HIVE_CONF:') 
		windowsScript.text = windowsScript.text.replace('set CLASSPATH=', 'set CLASSPATH=%HIVE_CONF%;')
	}
}

repositories {
    jcenter()
}

dependencies {

    compile	'ch.qos.logback:logback-core:1.1.3',
    		'ch.qos.logback:logback-classic:1.1.3',
    		'org.slf4j:slf4j-api:1.7.12',
    		'org.apache.commons:commons-lang3:3.4',
    		'org.apache.commons:commons-dbcp2:2.1.1',
    		'commons-configuration:commons-configuration:1.10',
    		'commons-collections:commons-collections:3.2.1',	// optional, but needed, for XML support in commons-configuration
    		'commons-daemon:commons-daemon:1.0.15',
    		'com.google.inject:guice:4.0',
    		'com.google.guava:guava:18.0'

    testCompile	'junit:junit:4.12'

}

processResources {
    filter ReplaceTokens, tokens: ['version': project.version]
}
