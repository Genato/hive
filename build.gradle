import org.apache.tools.ant.filters.*

plugins {
	id 'java'
	id 'application'
}

sourceCompatibility = 1.8
version = '0.0.0-pre'
mainClassName = 'hr.vsite.hive.HiveCmd'

run {
	classpath += files("${System.env.HIVE_CONF}")
}

startScripts {
	doLast {
		unixScript.text = unixScript.text.replace('CLASSPATH=', 'CLASSPATH=$HIVE_CONF:$APP_HOME/conf:') 
		windowsScript.text = windowsScript.text.replace('set CLASSPATH=', 'set CLASSPATH=%HIVE_CONF%;%APP_HOME%\\conf;')
	}
}

repositories {
    jcenter()
    mavenCentral()
}

configurations {
	provided
}

dependencies {

	compile	'ch.qos.logback:logback-core:1.1.3',
			'ch.qos.logback:logback-classic:1.1.3',
			'org.slf4j:slf4j-api:1.7.12',
			'org.apache.commons:commons-lang3:3.4',
			'org.apache.commons:commons-dbcp2:2.1.1',
			'commons-configuration:commons-configuration:1.10',
			'commons-collections:commons-collections:3.2.1',	// optional, but needed, for XML support in commons-configuration
			'commons-daemon:commons-daemon:1.0.15',
			'com.google.inject:guice:4.0',
			'com.google.inject.extensions:guice-servlet:4.0',
			'com.google.guava:guava:18.0',
			'org.quartz-scheduler:quartz:2.2.2',
			'org.eclipse.jetty.aggregate:jetty-all:9.3.5.v20151012',
			'org.jboss.resteasy:resteasy-jaxrs-all:3.0.13.Final',
			'org.jboss.resteasy:resteasy-servlet-initializer:3.0.13.Final',
			'org.jboss.resteasy:resteasy-guice:3.0.13.Final',
			'com.sun.faces:jsf-api:2.2.12',
			'com.sun.faces:jsf-impl:2.2.12',
			'javax.el:el-api:2.2',
			'org.glassfish.web:el-impl:2.2',
			'javax.servlet.jsp:jsp-api:2.2'

	provided 'javax.servlet:javax.servlet-api:3.1.0'

    testCompile	'junit:junit:4.12'

}

processResources {
    filter ReplaceTokens, tokens: ['version': project.version]
}

distributions {
	main {
		contents {
			
			exclude('conf/hive.xml')
			rename('hive.template.xml', 'hive.xml')
			
			exclude('conf/logback.xml')
			rename('logback.template.xml', 'logback.xml')
			
			exclude('conf/jdbc.properties')
			rename('jdbc.template.properties', 'jdbc.properties')
			
			exclude('conf/jetty.xml')
			rename('jetty.template.xml', 'jetty.xml')
			
		}
	}
}
